// Get PCB Entry +  Exit Process + Free User Area Page + Free Page Table

alias functionNum R1;
alias currentPID R2;

// ------------------------------------- Get PCB Entry  ----------------------------------------------------------- //
if (functionNum == 1) then
   // Loops until free Process Table entry found, ie. STATE = TERMINATED
   alias i R3;
   i = 0;

   while (i != 16 && [PROCESS_TABLE + (i*16) + 4] != TERMINATED) do
      i = i+1;
   endwhile;

   // print "pcb entr";
   // print i;
   
   if (i == MAX_PROC_NUM) then
      R0 = -1;
   else
      [PROCESS_TABLE + (i*16) + 4] = ALLOCATED;                   // State
      [PROCESS_TABLE + (i*16) + 1] = i;                           // PID
      [PROCESS_TABLE + (i*16) + 14] = PAGE_TABLE_BASE + (i*20);   // PTBR
      [PROCESS_TABLE + (i*16) + 15] = 10;                         // PTLR
      R0 = i;
   endif;
   return;
endif;   

// ------------------------------------- Free User Area Page ------------------------------------------------------ //
if (functionNum == 2) then
   alias userAreaPage R3;
   userAreaPage = [PROCESS_TABLE + currentPID*16 + 11];

   alias i R4;
   alias resourceTableBase R5;
   i = 0;                       // 496
   resourceTableBase = userAreaPage*512 + RESOURCE_TABLE_OFFSET;

   while (i < 16) do 
      if ([resourceTableBase + i] == 1) then
         multipush (R1, R2, R3, R4, R5);
         R1 = 7;                                    // Release Semaphore
         R2 = [resourceTableBase + i + 1];          // Semaphore Table Index
         R3 = [SYSTEM_STATUS_TABLE+1];               
         call RESOURCE_MANAGER;
         multipop (R1, R2, R3, R4, R5);
      else
         if ([resourceTableBase + i] == 0) then
            multipush (R1, R2, R3, R4, R5);
            R1 = 4;                                  // Close function
            R2 = [resourceTableBase + i + 1];        // Open File Table Index              
            call FILE_MANAGER;
            multipop (R1, R2, R3, R4, R5);
         endif;
      endif;
      i = i + 2;
   endwhile;

   multipush (R1, R2, R3);
   R1 = 2;        // Release page
   R2 = userAreaPage;
   call MEMORY_MANAGER;
   multipop (R1, R2, R3);
   return;
endif;

// ------------------------------------- Exit Process ------------------------------------------------------------- //
// Exit Process needs to wake up all waiting for it ONLY if not called from Exec system call
// MODE flag is checked to find which system call invoked Exit Process

if (functionNum == 3) then
   multipush (R1, R2);
   R1 = 4;      // Free page table
   R2 = currentPID;
   call PROCESS_MANAGER;
   R1 = 2;      // Free user area page
   R2 = currentPID;
   call PROCESS_MANAGER;
   multipop (R1, R2);

   // Waking up processes if system call number is not 9
   if ([PROCESS_TABLE + currentPID*16 + 9] != 9) then 
      alias i R3;
      i = 0;
      while (i < 16) do
        if ([PROCESS_TABLE + i*16 + 4] == WAIT_PROCESS && [PROCESS_TABLE + i*16 + 5] == currentPID) then
           [PROCESS_TABLE + i*16 + 4] = READY;
           [PROCESS_TABLE + i*16 + 5] = -1;
        endif;
        
        // Checking for cases where PPID of a process is PID of current process, then invalidating
        if ([PROCESS_TABLE + i*16 + 2] == currentPID) then
           [PROCESS_TABLE + i*16 + 2] = -1;
        endif;

        i = i+1;
      endwhile;
   endif;

   [PROCESS_TABLE + currentPID*16 + 4] = TERMINATED;
   return;
endif;

//  ------------------------------------ Free Page Table --------------------------------------------------------- //
if (functionNum == 4) then
   // Invalidating library pages by setting to -1 and 0000
   [PAGE_TABLE_BASE + currentPID*20 + 0] = -1;
   [PAGE_TABLE_BASE + currentPID*20 + 1] = "0000";
   [PAGE_TABLE_BASE + currentPID*20 + 2] = -1;
   [PAGE_TABLE_BASE + currentPID*20 + 3] = "0000";
   
   // Calling Release page for each valid entry
   alias i R3;
   i = 4;
   while (i < 20) do
      alias ptbr R4;      // IMPORTANT: PTBR != ptbr
      ptbr = PAGE_TABLE_BASE + currentPID*20;
      if ([ptbr + i] != -1) then
         multipush (R1, R2, R3, R4);
         R1 = 2;
         R2 = [ptbr + i];
         call MEMORY_MANAGER;
         multipop (R1, R2, R3, R4);
        
         [ptbr + i] = -1;
         [ptbr + i + 1] = "0000";
       endif;
       i = i+2;
   endwhile;
   
   alias diskEntry R4;
   diskEntry = DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE+1]*10;          // Disk map table start of current process
   i = 0; 
   while (i < 10) do
      if (i == 2 || i == 3 || i == 8 || i == 9) then                 // Checking the heap and stack entries
         // breakpoint;
         if ([diskEntry + i] != -1) then
            multipush (R1, R2, R3, R4, R5);
            R1 = 1;
            R2 = [diskEntry + i];
            call MEMORY_MANAGER;             // Release block
            multipop (R1, R2, R3, R4, R5);
         endif;
         [diskEntry + i] = -1;
      endif;
      i = i+1;
   endwhile;   
return;
endif;