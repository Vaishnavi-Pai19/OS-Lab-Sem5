// Exec system call number = 9, interrupt routine 9

// Saving user stack value for later use
alias userSP R0;
userSP = SP;

// User to Kernel Stack
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 13] = SP;
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 -1;

// MODE flag to system call of Exec
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 9] = 9;

// Getting argument (name of file) from user stack
alias physicalPageNo R1;
alias offset R2;
alias fileName R3;

physicalPageNo = [PTBR + 2 * ((userSP - 4)/512)];   // Arg 1 contains file name
offset = (userSP - 4) % 512;
fileName = [(physicalPageNo * 512) + offset];
// breakpoint;    

// Searching inode table for the file
alias i R4;
alias inodeIndex R5;
i = 0;
inodeIndex = -1;
while (i < MAX_FILE_NUM) do
   if ([INODE_TABLE + i*16 + 1] == fileName) then       // Checking if file name matches
      if ([INODE_TABLE + i*16] == EXEC) then            // Checking if file in XEXE format
         inodeIndex = i;
      endif;
      break;
   endif;
   i = i+1;
endwhile;

// Indicating failure
if (inodeIndex == -1) then                              // Does not exist in inode table
   [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 9] = 0;           // Changing MODE to User
   [[PTBR + 2*((userSP - 1)/512)]*512 + ((userSP - 1)%512)] = -1;    // -1 in return value	
   SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 13];	     // Resetting stack pointer
   ireturn;                                                          // Returning to user mode
endif;

// Calling Exit process
multipush (R0, R1, R2, R3, R4, R5);
R1 = 3;
R2 = [SYSTEM_STATUS_TABLE + 1];
call PROCESS_MANAGER;
multipop (R0, R1, R2, R3, R4, R5);

// Reclaiming User Area Page from Process Table of current process
alias userAreaPageNo R6;
userAreaPageNo = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11];
[MEMORY_FREE_LIST + userAreaPageNo] = 1;                     // Reclaiming page
[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2]-1;     // Decrementing the MEM_FREE_COUNT field

// Initializing the kernel stack of new process
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 - 1;   // SP to the top of User Page Area
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 4] = RUNNING;       // New process uses PID of terminated process
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 7] = inodeIndex;    // Updating inode index of process table

// Allocating new pages and setting page table entries for new process
PTBR = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1] * 16) + 14];

// Library pages - read only
[PTBR+0] = 63;
[PTBR+1] = "0100";
[PTBR+2] = 64;
[PTBR+3] = "0100";

multipush (R0, R1, R2, R3, R4, R5, R6, PTBR);
R1 = 1;       // Get 2 free pages for heap
call MEMORY_MANAGER;
[PTBR+4] = R0;
[PTBR+5] = "0110";
R1 = 1;
call MEMORY_MANAGER;
[PTBR+6] = R0;
[PTBR+7] = "0110";

R1 = 1;       // Get 2 free pages for stack
call MEMORY_MANAGER;
[PTBR+16] = R0;
[PTBR+17] = "0110";
R1 = 1;
call MEMORY_MANAGER;
[PTBR+18] = R0;
[PTBR+19] = "0110";

multipop (R0, R1, R2, R3, R4, R5, R6, PTBR);

// Code
[PTBR+8]  = -1;
[PTBR+9]  = "0000";
[PTBR+10] = -1;
[PTBR+11] = "0000";
[PTBR+12] = -1;
[PTBR+13] = "0000";
[PTBR+14] = -1;
[PTBR+15] = "0000";

// Updating page table entried based on code pages
alias j R7;
j = 0;
while (j < 4) do
   if ([INODE_TABLE + (inodeIndex*16) + 8 + j] != -1) then
      multipush (R0, R1, R2, R3, R4, R5, R6, R7, PTBR);
      R1 = 1;        // Get free page
      call MEMORY_MANAGER;
      [PTBR + j*2 + 8] = R0;
      [PTBR + j*2 + 9] = "0100";
      multipop (R0, R1, R2, R3, R4, R5, R6, R7, PTBR);
      loadi ([PTBR + 8 + 2*j], [INODE_TABLE + (inodeIndex*16) + 8 + j]);
   endif;
   j = j+1;
endwhile;

// Storing entry point IP to top of user stack
[[PTBR + 16]*512] = [[PTBR + 8]*512 + 1];
SP = 8*512;      // SP back to user stack

// MODE flag back to user mode and returning to user mode
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 9] = 0;
ireturn;

